<html>
	<head>
		<title>Volumetric Satisfaction Interval Utility</title>
        <meta name="description" content="Utility to create Interval List AWB component containing intervals for when any part of a Volumetric is satisfied.">
        
        <script language="vbscript">
			Dim app
			Dim root
			Dim scen
			Set app = window.external
			Set root = app.Personality2
			Set scen = root.CurrentScenario
			
            Function genReport()
				Dim volumetricName
				Dim timeStep
				Dim componentName
				volumetricName = CStr(volName.value)
				timeStep = CDbl(timeStepInit.value)
				intervalListName = CStr(compName.value)
				
				' Grab volumetric object and recompute
			
				Dim volumetric
				Set volumetric = root.GetObjectFromPath("*/Volumetric/" + volumetricName)
				volumetric.Compute()

				' Get satisfaction data providers from volumetric
				root.UnitPreferences.SetCurrentUnit "DateFormat", "EpSec"
				Dim satisfactionData
				Set satisfactionData = volumetric.DataProviders("Satisfaction Volume").Exec(scen.StartTime, scen.StopTime, timeStep)

				' Isolate times and percent satisfied
				Dim times 
				times = satisfactionData.DataSets.GetDataSetByName("Time").GetValues()
				Dim satisfied
				satisfied = satisfactionData.DataSets.GetDataSetByName("Percent Satisfied").GetValues()

				' Set counting variables and empty arrays
				Dim i
				i = 0
				Dim intervalCount
				intervalCount = 0
				Dim previous
				' Initialize array, assign bogus value to denote no change
				ReDim intervalArray(0)
				intervalArray(0) = -1

				' For each value returned in the data provider, check for nonzero. 
				' Store times where the satisfaction switches from 0 to nonzero and nonzero to 0. Edge cases taken care of using If statements
				For Each value in satisfied
					If (i = 0) Then
						previous = value
						If (previous <> 0) Then
							intervalArray(0) = times(i)
						End If
						i = i + 1
					Else 
						If (previous = 0) And value <> 0 Then
							If (intervalArray(0) = -1) Then
								intervalArray(0) = times(i)
							Else 
								ReDim Preserve intervalArray(UBound(intervalArray) + 1)
								intervalArray(UBound(intervalArray)) = times(i)

							End If
						ElseIf (previous <> 0) And (value = 0) Then
							ReDim Preserve intervalArray(UBound(intervalArray) + 1)
							intervalArray(UBound(intervalArray)) = times(i)
						End If
						previous = value
						i = i + 1
					End If
				Next

				If scen.Vgt.EventIntervalLists.Contains(intervalListName) Then
					scen.Vgt.EventIntervalLists.Remove(intervalListName)
				End If

				Dim intervals
				Set intervals = scen.Vgt.EventIntervalLists.Factory.CreateEventIntervalListFixed(intervalListName, "Volumetric satisfaction interval")
				intervals.SetIntervals(intervalArray)
				
            End Function


        </script>


	</head>
	<link href="../../htmlUtilities.css" rel="stylesheet" type="text/css" id="theStyle">
	<body>
	<h3> Volumetric Satisfaction Interval Utility </h3>
        Utility to create Interval List AWB component containing intervals for when any part of a Volumetric is satisfied by a spatial conditions used in a constrained spatial grid. 
		This utility takes the name of the Volumetric object to be used, a name for the AWB Interval component and a time step that determines the absolute accuracy of the intervals. 
		The Interval List component will be nested under the Scenario object in AWB. 
		<br/><br/>
		<hr>
		<table border ="0" class="task_box" cellpadding="6" cellspacing="0">
			<tr>
                <td>Volumetric:</td>
                <td width="90%"><input type="text" name="volName" style="width: 90%"></td>	
            </tr>
			<tr>
                <td>Interval List Name:</td>
                <td width="90%"><input type="text" name="compName" style="width: 90%"></td>	
            </tr>
            <tr>
                <td>Time Step (sec):</td>
                <td width="10%"><input type="text" name="timeStepInit" style="width: 10%"></td>	
            </tr>
            <td colspan="3"><input type="button" name="Create List" value="Create List" onClick=genReport() /></td>
		</table>
	<hr />

	</body>

</html>